<INICIO> ::= <InstructionES>

<InstructionES> ::= <InstructionES> <Instruction>
                  | <Instruction>

<Instruction> ::= <declaracion>
               | <asignacion>
               | <append>
               | <print>
               | <if>
               | <increaseDecrease>
               | <for>
               | <while>
               | <do>
               | <match>
               | <break>
               | <returnn>
               | <continue>
               | <declaracionEstructuraD>
               | <struct>
               | <methodd>
               | <start_whit>
               | <call>
               | "error" ";"
               | "error"

<EXPRESION> ::= "-" <EXPRESION> %prec UMINUS
              | "!" <EXPRESION> %prec UNEGATIVE
              | <EXPRESION> "+" <EXPRESION>
              | <EXPRESION> "-" <EXPRESION>
              | <EXPRESION> "/" <EXPRESION>
              | <EXPRESION> "*" <EXPRESION>
              | <EXPRESION> "%" <EXPRESION>
              | <EXPRESION> "^" <EXPRESION>
              | <EXPRESION> "==" <EXPRESION>
              | <EXPRESION> "!=" <EXPRESION>
              | <EXPRESION> "<" <EXPRESION>
              | <EXPRESION> "<=" <EXPRESION>
              | <EXPRESION> ">" <EXPRESION>
              | <EXPRESION> ">=" <EXPRESION>
              | <EXPRESION> "&&" <EXPRESION>
              | <EXPRESION> "||" <EXPRESION>
              | <EXPRESION> "xor" <EXPRESION>
              | "(" <EXPRESION> ")"
              | <varCasteo> <EXPRESION> %prec CAST
              | "ENTERO"
              | "DECIMAL"
              | "CADENA"
              | "CHARAC"
              | "BOOL"
              | "ID"
              | "ID" "[" <EXPRESION> "]"
              | "ID" "[" <EXPRESION> "]" "[" <EXPRESION> "]"
              | "ID" "." "remove" "(" <EXPRESION> ")"
              | "length" "(" "ID" ")"
              | "ID" "." "find" "(" <EXPRESION> ")"
              | "tustrin" "(" <EXPRESION> ")"
              | "round" "(" <EXPRESION> ")"
              | <callEXP>
              | "ID" "." "ID"
              | "ID" "." "ID" "." "ID"
              | "{" <valuesStruct> "}"

<methodd> ::= "void" "ID" "(" <parametersFuncts> ")" "{" <InstructionES> "}"
            | "void" "ID" "(" ")" "{" <InstructionES> "}"
            | <typeVar> "ID" "(" <parametersFuncts> ")" "{" <InstructionES> "}"
            | <typeVar> "ID" "(" ")" "{" <InstructionES> "}"

<parametersFuncts> ::= <parametersFuncts> ":" <typeVar> "ID"
                     | <typeVar> "ID"

<callEXP> ::= "ID" "(" <callsParameters> ")"
            | "ID" "(" ")"

<call> ::= "ID" "(" <callsParameters> ")" ";"
         | "ID" "(" ")" ";"

<start_whit> ::= "start_w" "ID" "(" <callsParameters> ")" ";"
               | "start_w" "ID" "(" ")" ";"

<callsParameters> ::= <callsParameters> ":" <EXPRESION>
                    | <EXPRESION>

<append> ::= "ID" "." "append" "(" <EXPRESION> ")" ";"

<varCasteo> ::= "(" "int" ")"
              | "(" "double" ")"
              | "(" "char" ")"
              | "(" "string" ")"

<if> ::= "if" "(" <EXPRESION> ")" "{" <InstructionES> "}" "else" <if>
       | <elsees>
       | <simpleIf>

<for> ::= "for" "(" <declaraAsigna> <EXPRESION> ";" <updateVarFor> ")" "{" <InstructionES> "}"

<updateVarFor> ::= "ID" "++"
                 | "ID" "--"
                 | "ID" "=" <EXPRESION>

<while> ::= "while" "(" <EXPRESION> ")" "{" <InstructionES> "}"

<do> ::= "do" "{" <InstructionES> "}" "while" "(" <EXPRESION> ")" ";"

<match> ::= "match" <EXPRESION> "{" <listCases> "}"

<listCases> ::= <listCases> <case>
              | <case>

<case> ::= <EXPRESION> "=>" "{" <InstructionES> "}"
         | "default" "=>" "{" <InstructionES> "}"

<declaraAsigna> ::= <declaracion>
                  | <asignacion>

<cierreElse> ::= "else" "{" <InstructionES> "}"

<simpleIf> ::= "if" "(" <EXPRESION> ")" "{" <InstructionES> "}"

<elsees> ::= "if" "(" <EXPRESION> ")" "{" <InstructionES> "}" <cierreElse>

<print> ::= "print" "(" <EXPRESION> ")" ";"

<declaracion> ::= <mutabilidad> "ID" ":" <typeVar> ";"
               | <mutabilidad> "ID" ":" <typeVar> "=" <EXPRESION> ";"
               | <mutabilidad> "ID" ":" "ID" "=" "{" <valuesStruct> "}" ";"

<declaracionEstructuraD> ::= <mutabilidad> "ID" ":" <typeVar> "[]" "=" "[" <valoresVectorAlone> "]" ";"
                           | <mutabilidad> "ID" ":" <typeVar> "[][]" "=" "[" <valoresVector2D> "]" ";"
                           | "list" "<" <typeVar> ">" "ID" "=" "new" "list" "(" ")" ";"

<valoresVector2D> ::= "[" <valoresVectorAlone> "]"
                    | <valoresVector2D> ":" "[" <valoresVectorAlone> "]"

<valoresVectorAlone> ::= <EXPRESION>
                       | <valoresVectorAlone> ":" <EXPRESION>

<asignacion> ::= "ID" "=" <EXPRESION> ";"
               | "ID" "[" <EXPRESION> "]" "=" <EXPRESION> ";"
               | "ID" "[" <EXPRESION> "]" "[" <EXPRESION> "]" "=" <EXPRESION> ";"
               | "ID" "." "ID" "=" <EXPRESION> ";"
               | "ID" "." "ID" "." "ID" "=" <EXPRESION> ";"

<increaseDecrease> ::= "ID" "++" ";"
                     | "ID" "--" ";"

<mutabilidad> ::= "var"
                | "const"

<typeVar> ::= "string"
            | "int"
            | "double"
            | "char"
            | "boolean"

<break> ::= "break" ";"

<continue> ::= "continue" ";"

<returnn> ::= "return" ";"
            | "return" <EXPRESION> ";"

<struct> ::= "struct" "{" <paramsStructs> "}" "ID" ";"

<paramsStructs> ::= <paramsStructs> "ID" ":" <structsTypes> ";"
                  | "ID" ":" <structsTypes> ";"

<structsTypes> ::= <typeVar>
                 | "ID"

<valuesStruct> ::= <valuesStruct> ":" "ID" ":" <EXPRESION>
                 | "ID" ":" <EXPRESION>
